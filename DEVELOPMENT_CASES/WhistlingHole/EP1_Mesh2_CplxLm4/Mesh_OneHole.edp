// Mesh generator for the flow across a hole
//                           Lext      
//         Lc         ----------------------    
//      ----------  |                      |
//      |        |  |                      |
//      |        |ep|                      | Rext
// Rc   |        ---                       |
//      |         Rh                       |
//      |                                  |
//      -----------------------------------

// Aspect ratio beta = ep/(2*Rh)
	
	
	cout << "### Mesh generation for the flow across a single hole" << endl;
	
	include "Macros_StabFem.edp" 

	real Rhole=1;
	real Rext,Lext, Rcav,Lcav;
	real chi, ep ; // epaisseur	
	cout << " Enter value of chi ? " ;
	cin >> chi ;
	cout << " => chi = " << chi << endl; 
    cout << " Enter value of Rext, Lext, Rcav,Lcav ? " ;
	cin >> Rext ;
	cout << " => Rext = " << Rext << endl; 
    cin >> Lext ;
	cout << " => Lext = " << Lext << endl; 
    cin >> Rcav ;
	cout << " => Rcav = " << Rcav << endl; 
    cin >> Lcav ;
	cout << " => Lcav = " << Lcav << endl; 

	ep = 2*chi;
	int Ni = 2;

	//symetry axis
	border axis(t=-(Lcav+ep),Lext){x=t ;y=0; label=6;};
	
    //outlet
	border outlet(t=0,Rext-1){x=Lext; y=t; label=3;};
	
    //outlet
	border outletP(t=Rext-1,Rext){x=Lext; y=t; label=31;};
	
    //upperout
    border upperout(t=0,Lext){x=Lext-t; y=Rext; label=5;};
	
    //wallout
    border wallout(t=Rext,Rhole){x=0; y=t; label=2;};

    //wallb
    border wallb(t=0,ep){x=-t; y=Rhole; label=2;};
	
    //wallin
    border wallin(t=Rhole,Rcav){x=-ep; y=t; label=2;};
	
   //upperinlet
   border upperin(t=0,Lcav){x=-ep-t; y=Rcav; label=7;};

    //inlet
	border inlet(t=0,Rcav){x=-ep-Lcav; y=Rcav-t; label=1;};
	        	
    IFMACRO(FREEFEMPLOTS,YES)
	plot(
	  axis(Ni*(Lext+Lcav+ep))  
	+ outlet(Ni*Rext) + upperout(Ni*Lext) 
	+ wallout(Ni*(Rext-Rhole)) + wallb(Ni*ep) 
	+ wallin(Ni*(Rcav-Rhole)) + upperin(Ni*Lcav) + inlet(Ni*Rcav) 
	, wait=1);
    ENDIFMACRO

	//MESHING

	mesh th;
	th = buildmesh(  axis(Ni*(Lext+Lcav+ep))  
	+ outlet(Ni*Rext) + outletP(Ni) + upperout(Ni*Lext) 
	+ wallout(Ni*(Rext-Rhole)) + wallb(Ni*ep+1) 
	+ wallin(Ni*(Rcav-Rhole)) + upperin(Ni*Lcav) + inlet(Ni*Rcav) 
	);	

IFMACRO(FREEFEMPLOTS,YES)	
	plot(th, wait=1);
ENDIFMACRO

// SAVE THE MESH in mesh.msh file 
savemesh(th,ffdatadir+"mesh.msh");


// FIRST AUXILIARY FILE for Stabfem : SF_Init.ff2m
{
            ofstream file(ffdatadir+"SF_Init.ff2m"); 
            file <<  "### Data generated by Freefem++ ; " << endl;
            file << "(Auxiliary file with information on initial mesh)" << endl;
            file << "problemtype AxiXRCOMPLEX" << endl;
			file << "real Rhole real Rext real Lext real Rcav real Lcav real ep " << endl;
			file <<  Rhole  << endl << Rext << endl << Lext << endl << Rcav << endl << Lcav << endl << ep  <<  endl;
			
}

// SECOND AUXILIARY FILE  for Stabfem : mesh.ff2m
	SFWriteMesh(ffdatadir+"mesh.ff2m",th)


// THIRD AUXILIARY FILE for Stabfem : SF_Geom.edp
{ 
 real SFGeomRext = Rext ; 
 real SFGeomLext = Lext ; 
 real SFGeomep = ep ; 
 real SFGeomRcav = Rcav ; 
 real SFGeomLcav = Lcav ;
 real SFGeomRhole = Rhole ;
			ofstream file2("SF_Geom.edp"); 
			file2 << "// Description geometry (file automatically created ; to be included in the FreeFem programs)" << endl;
            file2 << " real SFGeomLengthscale = 2 ; // length scale for nondimensionalization" << endl ;
            file2 << " real SFGeomVelocityscale = 1 ; // velocity scale for nondimensionalization"  << endl ;
            file2 << " real SFGeomRext = " << Rext << " ; " << endl ; 
 			file2 << " real SFGeomLext = " << Lext << " ; " << endl ; 
 			file2 << "real SFGeomep = " << ep << " ; " << endl; 
			file2 << "real SFGeomRcav = " << Rcav << " ; " << endl; 
			file2 << " real SFGeomLcav = " << Lcav << " ; " << endl ;
			file2 << " real SFGeomRhole = " << Rhole << " ; " << endl;
}


//




// CREATE A guess for the base flow (here starting from zero works very well)
 		fespace femp1(th,P1);
        fespace femp2xfemp2xfemp1(th,[P2,P2,P1]);
IFMACRO(COORDINATEMAPPING,YES)
        femp2xfemp2xfemp1<complex> [ux,ur,up]=[0,0,0];
ENDIFMACRO
IFMACRO(COORDINATEMAPPING,NO)
        femp2xfemp2xfemp1 [ux,ur,up]=[0,0,0];
ENDIFMACRO
IFMACRO(!COORDINATEMAPPING)
        femp2xfemp2xfemp1 [ux,ur,up]=[0,0,0];
ENDIFMACRO
        real Re = 0.1;real nu=1; // these two are useless at this stage but required in the macros 
        
// Save in txt format (for FreeFem)        
      	{
      	ofstream file(ffdatadir+"BaseFlow_guess.txt");
	    file << ur[] << endl;
	    file <<  endl << 0 << endl;
      	}
      	
      	
// Save in ff2m format (for StabFem)	    
     SFWriteBaseFlow(ffdatadir+"BaseFlow.ff2m",u,"BaseFlow",0);
	     




		cout << "............................................" << endl;
		cout << "............................................" << endl;
		cout << "Maillage termine " << endl << "..." << endl;
		cout << "............................................" << endl;
		cout << "............................................" << endl;

