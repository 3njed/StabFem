
/// Example for Freefem_to_matlab
/// Version D.F. december 2017


verbosity=0; 
border a(t=0,1){x=t;y=0;label=1;}; 
border b(t=0,0.5){x=1;y=t;label=1;}; 
border c(t=0,0.5){x=1-t;y=0.5;label=1;}; 
border d(t=0.5,1){x=0.5;y=t;label=1;}; 
border e(t=0.5,1){x=1-t;y=1;label=1;}; 
border f(t=0,1){x=0;y=1-t;label=1;}; 
//func uu= sin(y*pi)/10; func vv= cos(x*pi)/10;

int nn=4; // mesh density
mesh th = buildmesh ( a(6*nn) + b(4*nn) + c(4*nn) +d(4*nn) + e(4*nn) + f(6*nn));
savemesh(th,"mesh.msh"); // freefem

// old syntax : mesh.mshinfo
{
ofstream file("mesh.mshinfo"); 
file << "Mesh for a L-shaped domain" << endl;
file << "PROBLEM TYPE " << endl;
file << "SIMPLE_EXAMPLE" << endl;
file << "PARAMETERS" << endl;
file << "nn " << endl;
file << nn << endl;
}

// New syntax : SF_Init.ff2m
{
            ofstream file("SF_Init.ff2m"); 
			file << "Defininition of problem type and geometry for StabFem. Problem type : " << endl;
			file << "EXAMPLE" << endl;
			file << "Format :  (this list may contain geometrical parameters such as domain dimensions, etc..)" << endl;
			file << "real R " << endl;
			file << 1  << endl;
}

// New syntax : mesh.ff2m
{
            ofstream file("mesh.ff2m"); 
			file << "Mesh generated by FreeFem++ ; mesh type :" << endl;
			file << "2D" << endl;
			file << "Format : " << endl;
			file << "int np int nt int density" << endl;
			file << th.nv  << " " << th.nt << " " << nn << endl;
}



// First sample problem (consider it as base flow) : steady conduction 

fespace Vh(th,P1);
Vh u,v;
func so= 1.0;
real cpu=clock();
solve Poisson(u,v,solver=LU)=int2d(th)(dx(u)*dx(v) + dy(u)*dy(v)) - int2d(th)( so*v)+on(a,b,c,d,e,f,u=0);
//plot(th,u);


{ 
	ofstream file("BaseFlow.ff2m"); 
    file << "### Data generated by Freefem++ ; " << endl;
    file << "Temperature field in a L-shaped region ; steady conduction with constant volume source" << endl;
    file << "Format : " << endl;
	file << "P1 T" << endl << endl ; 
	for (int j=0;j<u[].n ; j++)
		file << u[][j] << endl;
}


// Second sample problem : unsteady conduction
real omega = 50;
Vh<complex> uC,vC;
cpu=clock();
solve PoissonU(uC,vC,solver=LU)=int2d(th)(dx(uC)*dx(vC) + dy(uC)*dy(vC)) - int2d(th)(1i*omega*uC*vC)+on(a,b,c,d,e,f,uC=1);
//plot(Th,uC);

Vh normgradUC = sqrt(abs(dx(uC))^2+abs(dy(uC))^2);
Vh normUC = abs(uC);

{ 
	ofstream file("Heat_unsteady.ff2m"); 
    file << "### Data generated by Freefem++ ; " << endl;
    file << "Temperature field in a L-shaped region ; steady conduction with constant volume source" << endl;
    file << "Format : " << endl;
	file << "real omega P1c Tc P1 normTc" << endl << endl ; 
    file << omega << endl ;
	for (int j=0;j<uC[].n ; j++)
		file << real(uC[][j]) << " " << imag(uC[][j]) << endl;
    file << endl;
    for (int j=0;j<uC[].n ; j++)
		file << normUC[][j]  << endl;
}






cout << " CPU time = " << clock()-cpu << endl;
