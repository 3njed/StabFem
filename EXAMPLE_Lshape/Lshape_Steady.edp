/// Program Lshape_Steady.edp : resolution of the steady heat equation for the basic StabFem example
include "Macros_StabFem.edp";

// Read the mesh
mesh th = readmesh("mesh.msh");
fespace Vh(th,P1);

// Solve the Poisson Equation
Vh u,v;
func so= 1.0;
real cpu=clock();
solve Poisson(u,v,solver=LU)=int2d(th)(dx(u)*dx(v) + dy(u)*dy(v)) - int2d(th)( so*v)+on(1,u=0);

// Generates the data file for the StabFem driver with 2D mesh-organized data 
{ 
	ofstream file("Data.ff2m"); 
    file << "### Data generated by Freefem++ ; " << endl;
    file << "Temperature" << endl;
    file << "Format : " << endl;
	file << "P1 T" << endl << endl ; 
	for (int j=0;j<u[].n ; j++)
		file << u[][j] << endl;
}

// Generates a second data file for the StabFem driver with data on a "slice"
{ 
	ofstream file("Heat_1Dcut.ff2m"); 
    file << "### Data generated by Freefem++ ; " << endl;
    file << "Temperature" << endl;
    int NN = 101;
    file << "Format : " << endl;
	file << "real." << NN << " Xcut" << "  real." << NN << " Tcut" << endl << endl ; 
	for (int j=0;j<NN ; j++) { file << 1./(NN-1)*j << endl;} ;
	for (int j=0;j<NN ; j++) {file  << u(1./(NN-1)*j,.25) << endl;};
    file << endl;
}

// Freefem plot if required
IFMACRO(FREEFEMPLOTS,YES)
plot(th,u);
ENDIFMACRO